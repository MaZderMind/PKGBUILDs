# Maintainer: Sebastian Reimers <hallo@studio-connect.de>
pkgname=jack2
pkgver=14.6.0
pkgrel=1
arch=(armv7h x86_64)
backup=(etc/security/limits.d/99-audio.conf)
makedepends=('libffado' 'celt' 'opus' 'libsamplerate' 'libsndfile' 'fftw')
#options=(!strip)
source=("https://github.com/studio-connect/jack2/archive/$pkgver-alpha.tar.gz"
	'99-audio.conf'
	'40-hpet-permissions.rules')
md5sums=('f4a58df806e559e811fb80bffc618d64'
	 'ae65b7c9ebe0fff6c918ba9d97ae342d'
	 '471aad533ff56c5d3cbbf65ce32cadef')

_pyfix() {
  sed -i 's:bin/env python:bin/env python2:' \
    "$pkgdir/usr/bin/jack_control"
}

build() {
    cd $pkgname-$pkgver-alpha
    python2 waf configure --prefix=/usr --alsa
    python2 waf build
}

package() {
    cd $pkgname-$pkgver-alpha
    python2 waf install --destdir="$pkgdir"
    _pyfix
    install -Dm644 "$srcdir/99-audio.conf" \
      "$pkgdir/etc/security/limits.d/99-audio.conf"

    install -Dm644 "$srcdir/40-hpet-permissions.rules" \
      "$pkgdir/usr/lib/udev/rules.d/40-hpet-permissions.rules"
}
